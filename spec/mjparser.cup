
package rs.ac.bg.etf.pp1;
import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;


parser code {:
	
	boolean errorDetected;

	Logger log = Logger.getLogger(getClass());
	
	public void report_fatal_error(String message, Object info) throws java.lang.Exception {
		done_parsing();
		report_error(message, info);	
	}
	
	public void syntax_error(Symbol cur_token){
		report_error("\nSintaksna greska", cur_token);	
	}
	
	public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
		report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);	
	}
	
	public void report_error(String message, Object info) {
		errorDetected = true;
		StringBuilder msg = new StringBuilder(message);
		if(info instanceof Symbol)
			msg.append(" na liniji ").append( ((Symbol)info).left );		
		log.error(msg.toString());
	}
	
:}

init with {:
	errorDetected = false;
:}

/* za presretanje lexera kad god vrati jedan token */
scan with {:
	Symbol s = this.getScanner().next_token();
	if( s!=null && s.value != null)
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

terminal PROG, ENUM, CONST, PRINT, READ, RETURN, NEW;
terminal PLUS, MINUS, MUL, DIV, MOD, INC ,DEC, EQUAL;
terminal DOT, SEMI, COMMA, LPAREN, RPAREN, LBRACE, RBRACE, LBRACKET, RBRACKET;
terminal COMMENT, BOOL, STRING, VOID;
terminal String IDENT;
terminal Integer NUMBER;

nonterminal Statement, StatementList, DesignatorStatement, Designator;
nonterminal SignedTerm, Expr, Term, Factor, Instantiation;
nonterminal Addop, Mulop;
nonterminal MyArray, EnumUse;
nonterminal Program, Type;
nonterminal InstPrimitive, InstArray;
nonterminal VarDeclList, MethDeclList, VarDecl, MultipleVarDecl, MultipleVarDeclExpr, MethDecl, ArrayDecl,  EnumExpr, EnumExprList, EnumInst;
nonterminal PrintStmt, PrintParam;
nonterminal Return, FuncCall;
nonterminal ConstExpr, ConstInitializationList, ConstDecl;
nonterminal GlobalDeclList, GlobalDecl,  GlobalVarDecl;
nonterminal ParameterList, Parameter, ArgList;
nonterminal Comment, MethType, EndOfGlobalDeclarations, EnumDecl;

nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName;
nonterminal rs.etf.pp1.symboltable.concepts.Obj EnumBegin;

Program   ::= (Program) PROG ProgName:progName GlobalDeclList EndOfGlobalDeclarations LBRACE MethDeclList RBRACE;

ProgName ::= (ProgName) IDENT:progName;

EndOfGlobalDeclarations ::= (EndOfGlobalDeclarationsProduction) /* epsilon */ ;

GlobalDeclList ::= (GlobalDeclListProduction) GlobalDeclList GlobalDecl
				|  /* epsilon */
				;

GlobalDecl ::= (GVarDeclList) GlobalVarDecl
			|  (GEnumDeclList) EnumDecl
			|  (GConstDeclList) ConstDecl
			;

GlobalVarDecl  ::=  (GVarDecl) VarDecl
				| 	(GlobalVarDeclErrorComma) error COMMA:l {: parser.report_error("Izvrsen oporavak do prvog ',' u deklaraciji promenljive", null); :} 
				|   (GlobalVarDeclErrorSemi) error SEMI:l {: parser.report_error("Izvrsen oporavak do prve ';' u deklaraciji promneljive", null); :} 
				;

VarDeclList ::= (VarDeclListProduction) VarDeclList VarDecl
			| /* epsilon */
			;
			
MultipleVarDeclExpr ::= (SimpleVarDecl) IDENT:varName
					|   (SimpleArrayDecl) ArrayDecl
					;
					 			
MultipleVarDecl ::= (MultipleVarDeclProduction) MultipleVarDeclExpr COMMA MultipleVarDecl
				| 	(SingleVarDeclProduction) MultipleVarDeclExpr
				; 

VarDecl  ::= (MultipleVariableDecl) Type:varType MultipleVarDecl SEMI
		 ;

MethDeclList ::= (MethDeclListProduction) MethDeclList MethDecl
            |   /* epsilon */
			;

Return ::=  (ReturnProduction) RETURN Expr SEMI
				;
				
Parameter ::= (ParameterVarProduction) Type IDENT
			| (ParameterArrayProduction) MyArray
			;
				
ParameterList ::= (MultipleParameterListProduction) Parameter COMMA ParameterList
			  |   (SingleParameterListProduction) Parameter
			  |   /* epsilon */
			  ;

MethType ::= (NonVoidMethType) Type:methType
		 |	 (VoidMethType) VOID 
		 ;				

MethDecl ::= (MethDeclProduction) MethType IDENT:methodName LPAREN ParameterList RPAREN VarDeclList LBRACE StatementList RBRACE
		;

ArrayDecl ::= (ArrayDeclProduction) IDENT:arrayName LBRACKET RBRACKET ;

ConstExpr ::= (ConstExprProduction) IDENT EQUAL NUMBER;

ConstInitializationList ::= (MultipleConstExpr) ConstExpr COMMA ConstInitializationList
						|   (SingleConstExpr) ConstExpr
						;						

ConstDecl ::= (ConstDeclProduction) CONST Type ConstInitializationList SEMI;

EnumInst ::= (EnumConstValue) EQUAL NUMBER:enumConstantValue
			| (NoEnumInst) /* epsilon */
			;

EnumExpr ::= (EnumExpr) IDENT:enumConstantName EnumInst;

EnumExprList ::= (MultipleEnumExpr) EnumExpr COMMA EnumExprList 
			 | (SingleEnumExpr) EnumExpr
			 ;
EnumBegin ::= (EnumBegin) ENUM IDENT:enumName;			 
EnumDecl ::= (EnumDecl) EnumBegin LBRACE EnumExprList RBRACE;

StatementList ::= (StatementListProduction) StatementList Statement
				|  /* epsilon */
				;
PrintParam ::= (PrintParamProduction) COMMA NUMBER
			|   /* epsilon */
			;
PrintStmt ::=  (PrintStmtProduction) PRINT LPAREN Expr PrintParam RPAREN 
			;

Statement ::=	(DesigStatement) DesignatorStatement SEMI
		  	|   (ReadCall) READ LPAREN Designator RPAREN SEMI
		  	|	(PrintCall) PrintStmt SEMI
		  	|   (MethCall) FuncCall SEMI
		  	|   (ReturnStatement) Return
		  	|   (CommentStatement) Comment
		    ;
ArgList ::= (ArgumentListProduction) COMMA ArgList
		| /* epsilon */
		;
		    
FuncCall  ::=  (FuncCallProduction)  IDENT LPAREN Expr ArgList RPAREN;

DesignatorStatement ::=  (DesStatAssignment) Designator EQUAL Expr
					  |  (DesStatAssignmentError) error:l {: parser.report_error("Izvrsen oporavak u naredbi dodele do prve ';' " , null); :}
					  |	 (DesStatInc) Designator INC
					  |	 (DesStatDec) Designator DEC
					  ;

SignedTerm ::= (MinusTerm) MINUS Term
			|  (PlusTerm) Term
			;							 
Expr ::=(AddopExpr) SignedTerm Addop Term
	 |  (TermExpr) SignedTerm
	 ;

Term ::=  (MulopTerm) Factor Mulop Factor
	 |  (FactorTerm) Factor
	 ;

Type ::=  (TypeProduction) IDENT:typeName;

InstPrimitive ::= (InstPrimitiveProduction) NEW Type;

InstArray  	  ::= (InstArrayProduction) InstPrimitive LBRACKET Expr RBRACKET;

Instantiation ::= (InstPrimit) InstPrimitive
			    | (InstArr) InstArray;
		
Factor ::= (IdentFactor) IDENT
		 | (NumberFactor) NUMBER
		 | (StringFactor) STRING
		 | (GroupFactor) LPAREN Expr RPAREN
		 | (BoolFactor) BOOL
		 | (InstFactor) Instantiation
		 | (ArrayFactor) MyArray
		 | (EnumUseFactor) EnumUse
		 | (FuncCallFactor) FuncCall
		 ;
		
MyArray  ::= (MyArrayProduction) IDENT LBRACKET Expr RBRACKET;		

EnumUse   ::= (EnumUseProduction) IDENT DOT IDENT;
		
Designator	::=	(IdentDesignator) IDENT
			|	(EnumUseDesignator) EnumUse
			|   (MyArrayDesignator) MyArray
			;
		
Addop  ::= (PlusAddop) PLUS 
		|  (MinusAddop) MINUS
		;

Mulop  ::= (MulMulop) MUL
		|  (DivMulop) DIV
		|  (ModMulop) MOD
		;
			
Comment ::= (Comment) COMMENT;